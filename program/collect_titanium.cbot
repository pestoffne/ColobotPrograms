extern void object::CollectTitanium() {
	object item;
	EBoost e(this);
	
	for (;;) {
		e.check_energy_loop(0.50);
		e.check_shield(0.50);
		
		if (load == null or load.category != TitaniumOre) {
			if (load != null) {
				goto(space(position, 0));
				drop();
			}
			
			item = e.radar_wait(new RadarArgs(TitaniumOre));
			
			if (item == null) {
				message("All TitaniumOres has been converted", DisplayWarning);
				return;
			}
			
			goto(item.position);
			grab();
		}
		
		item = e.radar_wait(new RadarArgs(Converter));
		goto(item.position);
		drop();
		move(-2.5);
		item = e.radar_wait(new RadarArgs(Titanium, 0, 30, 0, 10));
		
		if (item == null) {
			message("No Titanium near me", DisplayWarning);
			return;
		}
		
		if (goto(item.position) != 0) {
			message("Failed going to Titanium", DisplayWarning);
			return;
		}
		
		e.grab_from_ground(Titanium);
		
		item = radar(BlueFlag);
		
		if (item == null) {
			goto(space(position));
			drop();
		} else {
			e.drop_to_helix(item.position);
		}
	}
}
