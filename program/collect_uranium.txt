extern void object::CollectUranium() {
	object item; point pos;
	EBoost e(this);
	
	for (;;) {
		if (load == null or load.category != UraniumOre) {
			if (load != null) {
				goto(space(position), 0);
				drop();
			}

			item = e.radar_wait(UraniumOre);
			goto(item.position);
			grab();
		}
		
		item = e.radar_wait(NuclearPlant);
		goto(item.position);
		drop();
		move(-2.5);
		
		while (item.energyCell == null or item.energyCell.category == UraniumOre) {
			wait(1);
		}
		
		goto(item.position);
		grab();

		item = radar(GreenFlag);
		
		if (item == null) {
			goto(space(position));
			drop();
		} else {
			e.drop_to_helix(item.position);
		}
		
		pos = position;
		e.check_energy();
		e.check_shield();

		if (distance2d(item.position, position) > 10) {
			goto(pos);
		}
	}
}
