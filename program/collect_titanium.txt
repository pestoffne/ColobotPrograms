extern void object::CollectTitanium() {
	object item; point pos;
	EBoost e(this);
	
	for (;;) {
		if (load == null or load.category != TitaniumOre) {
			if (load != null) {
				goto(space(position, 0));
				drop();
			}
			
			item = e.radar_wait(TitaniumOre);
			
			if (item == null) {
				message("All TitaniumOres has been converted", DisplayWarning);
				return;
			}
			
			goto(item.position);
			grab();
		}
		
		item = e.radar_wait(Converter);
		goto(item.position);
		drop();
		move(-2.5);
		
		item = e.radar_wait(Titanium, 0, 30, 0, 10);
		goto(item.position);
		grab();
		
		item = radar(BlueFlag);
		
		if (item == null) {
			goto(space(position));
			drop();
		} else {
			e.drop_to_helix(item.position);
		}
		
		pos = position;
		e.check_energy(0.25);
		e.check_shield(0.50);
		
		if (distance2d(position, pos) > 10.0) {
			goto(pos);
		}
	}
}
